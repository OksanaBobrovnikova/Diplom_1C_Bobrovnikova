#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	
	// регистр ВКМ_ВыполненныеКлиентуРаботы
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	// регистр ВКМ_ВыполненныеСотрудникомРаботы
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧаса КАК СтоимостьЧаса
	|ПОМЕСТИТЬ ВТ_СтоимостьЧаса
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтоимостьЧаса.СтоимостьЧаса КАК СтоимостьЧаса,
	|	ЕСТЬNULL(ВКМ_ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентЗаВыполненныеРаботы, 0) КАК ПроцентЗаВыполненныеРаботы,
	|	ЕСТЬNULL(ВКМ_ВКМ_УсловияОплатыСотрудниковСрезПоследних.НулевойПроцент, 0) КАК НулевойПроцент
	|ИЗ
	|	ВТ_СтоимостьЧаса КАК ВТ_СтоимостьЧаса
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Специалист) КАК
	|			ВКМ_ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ИСТИНА";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Специалист", Специалист);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		СтоимостьЧаса = Выборка.СтоимостьЧаса;
		НулевойПроцент = Выборка.НулевойПроцент;
		ПроцентЗаВыполненныеРаботы = Выборка.ПроцентЗаВыполненныеРаботы;
		
		Если ПроцентЗаВыполненныеРаботы = 0 Тогда
			Если НулевойПроцент = 0 ИЛИ НулевойПроцент = Ложь Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = СтрШаблон("В условиях оплаты сотруднику %1 не указан процент за выполненные работы", Специалист);
				Сообщение.Сообщить();
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ТекСтрокаВыполненныеРаботы из ВыполненныеРаботы Цикл
		
		// регистр ВКМ_ВыполненныеКлиентуРаботы
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.КоличествоЧасов = ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = СтоимостьЧаса * Движение.КоличествоЧасов;
		
		// регистр ВКМ_ВыполненныеСотрудникомРаботы
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = Специалист;
		Движение.ЧасовКОплате = ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = Движение.ЧасовКОплате * СтоимостьЧаса* ПроцентЗаВыполненныеРаботы / 100;
		
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!
	//
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда  
        Возврат;  
	КонецЕсли; 
	 
	Если ЭтоНовый() Тогда 
		НоваяЗапись = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		НоваяЗапись.ТекстСообщения = "Новый документ";
		НоваяЗапись.Записать();
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.Специалист
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	    Выборка = Запрос.Выполнить().Выбрать();
	    
	    Если Выборка.Следующий() Тогда
	    	ДатаРабот = Выборка.ДатаПроведенияРабот;
		    ВремяНачала = Выборка.ВремяНачалаРаботПлан;
		    ВремяОкончания = Выборка.ВремяОкончанияРаботПлан;
		    СпециалистНовый = Выборка.Специалист;
	    КонецЕсли;
	
	    //Изменилась дата
	    Если ДатаРабот <> ДатаПроведенияРабот Тогда
	    	НоваяЗапись = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		    НоваяЗапись.ТекстСообщения = СтрШаблон("В документе %1 изменилась дата", Ссылка);
		    НоваяЗапись.Записать();
	    КонецЕсли;
	
	    //Изменилось время начала
	    Если ВремяНачала <> ВремяНачалаРаботПлан Тогда
	    	НоваяЗапись = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
	    	НоваяЗапись.ТекстСообщения = СтрШаблон("В документе %1 изменилось время начала выполнения работ", Ссылка);
		    НоваяЗапись.Записать();
		КонецЕсли;
	
	    //Изменилось время окончания
	    Если ВремяОкончания <> ВремяОкончанияРаботПлан Тогда
	    	НоваяЗапись = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		    НоваяЗапись.ТекстСообщения = СтрШаблон("В документе %1 изменилось время начала окончания работ ", Ссылка);
		    НоваяЗапись.Записать();
	    КонецЕсли;
	
	    //Иззменился исполнитель
	    Если СпециалистНовый <> Специалист Тогда
	    	НоваяЗапись = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		    НоваяЗапись.ТекстСообщения = СтрШаблон("В документе %1 изменился исполнитель", Ссылка);
		    НоваяЗапись.Записать();
	    КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
