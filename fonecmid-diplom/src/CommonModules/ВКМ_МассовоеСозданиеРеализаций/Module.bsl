
#Область СлужебныеПроцедурыИФункции 

Функция ЗаполнениеТабличнойЧастиМассовогоСозданияАктов(Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Владелец КАК Владелец,
	|	ДоговорыКонтрагентов.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_Договора
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	МЕСЯЦ(РеализацияТоваровУслуг.Дата) = МЕСЯЦ(&Период)
	|	И ГОД(РеализацияТоваровУслуг.Дата) = ГОД(&Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Реализации.Ссылка КАК Реализация,
	|	ВТ_Договора.Ссылка КАК Договор,
	|	ВТ_Договора.Владелец КАК Контрагент,
	|	ВТ_Договора.Организация
	|ИЗ
	|	ВТ_Договора КАК ВТ_Договора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
	|		ПО ВТ_Договора.Ссылка = ВТ_Реализации.Ссылка.Договор
	|ГДЕ
	|	ВТ_Реализации.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	МассивСозданныхДокументов = Новый Массив;
	
	Для Каждого Строка из Выборка Цикл
		
		НовыйДокументРеализации = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
        НовыйДокументРеализации.Дата = КонецМесяца(Период);
		НовыйДокументРеализации.Ответственный = Пользователи.ТекущийПользователь();
		НовыйДокументРеализации.Договор = Строка.Договор;
		НовыйДокументРеализации.Контрагент = Строка.Контрагент;
		НовыйДокументРеализации.Организация = Строка.Организация;
		НовыйДокументРеализации.ВКМ_ВыполнитьАвтозаполнение();
				
		Если НовыйДокументРеализации.ПроверитьЗаполнение() Тогда 
			НовыйДокументРеализации.Записать(РежимЗаписиДокумента.Проведение);
			СтрокаОбработки = Новый Структура;
			СтрокаОбработки.Вставить("Договор", Строка.Договор);
			СтрокаОбработки.Вставить("ДокументРеализации", НовыйДокументРеализации.Ссылка);
			МассивСозданныхДокументов.Добавить(СтрокаОбработки);
		КонецЕсли;
			
	КонецЦикла;
    
   	Возврат МассивСозданныхДокументов;

КонецФункции

#КонецОбласти
